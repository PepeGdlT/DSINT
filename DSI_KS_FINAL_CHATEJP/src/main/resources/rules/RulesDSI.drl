package rules;

// Importacion de clases
import java.util.List;
import main.java.dsin.Objeto.*;
import main.java.dsin.Accion.*;
import main.java.dsin.Personaje.*;
import main.java.dsin.Estado.*;
import main.java.dsin.Comprobacion.*;
import rules.Constantes;

// Variable global para mensajes de salida
global List<String> mensajes;


// REGLAS GENERALES---------------------------------------------------------------------------------------------------------------------
	
	//CAMBIOS: 
		/*
		-cambiado ALAS_ICARO_DEDALO por ALAS_PLUMA_CERA
		-cambios en las reglas de escapar del laberiento
		*/
	//TODO 
		/*
		-Cambiar mensajes  
		-comprobar funcionamiento de reglas de icaro y dedalo - DONE
		*/
	//NEW
		/*
		-2.3
		*/

//LOS ESTADOS Y ACCIONES DEBEN IR CON INSTANCIAS DE PERSONAJES


// REGLAS PARA ACCIONES GENERALES
//
rule "0.1 Quitar Objeto"
when
    $s : Personaje()
    $o : Objeto()
    $posee : Poseer(sujeto == $s, poseido == $o)
    $quita : Quitar(sujeto == $s, quitado == $o)
then
    retract($posee);
    retract($quita);
end

rule "0.0 Convertir a heroe"
salience 2
when
	$h : Heroe()
	$p : Personaje(nombre == $h.getNombre(), this != $h)
	$favores : List() from collect (FavoreceA(favorecido == $p))
	$enojos : List() from collect (TieneEnojoCon(enojado == $p))
	$posesiones : List() from collect (Poseer(sujeto == $p))
then
	mensajes.add($h.getNombre() + " es el heroe de esta historia");
	for(FavoreceA favor : (List<FavoreceA>) $favores){
		insert(new FavoreceA(favor.getSujeto(), $h));
		retract(favor);
	}
	
	for(TieneEnojoCon enojo : (List<TieneEnojoCon>) $enojos){
		insert(new TieneEnojoCon(enojo.getSujeto(), $h));
		retract(enojo);
	}
	
	for(Poseer posesion : (List<Poseer>) $posesiones){
		insert(new Poseer($h, posesion.getPoseido()));
		retract(posesion);
	}
	
	retract($p)
end

rule "0.3 Personaje asesina a otro" 
when 
	$sujeto : Personaje()
	$asesinado : Personaje()
	$asesina : Asesinar(sujeto == $sujeto, asesinado == $asesinado)
	
then 
	mensajes.add($asesinado.getNombre() +" ha muerto a manos de " + $sujeto.getNombre()+ ".");
	insert(new Muerto($asesinado));
	insert (new Lootear($sujeto, $asesinado));
end


rule "0.4 Obtiene objeto por el favor de un personaje"
when
    $heroe : Heroe()
    $personaje : Personaje()
    $objeto : Objeto()
    $poseePersonaje : Poseer(sujeto == $personaje, poseido == $objeto)
    FavoreceA(sujeto == $personaje, favorecido == $heroe)
then
    mensajes.add($heroe.getNombre() +" obtiene "+ $objeto.getNombre() +" debido al favor de "+ $personaje.getNombre()+ ".");
    retract($poseePersonaje);
    insert(new Poseer($heroe, $objeto));
    
end


rule "0.5 Heroe muere"
when
	$sujeto : Heroe()
	$muerto : Muerto(afectado == $sujeto)
then
	mensajes.add($sujeto.getNombre() + " no ha conseguido su objetivo. Fin de su historia.");
	retract($sujeto);
end


rule "0.6.1 Liberar personaje"
when 
	$libertador : Personaje()
	$liberado : Personaje()
	$liberar : Liberar(sujeto == $libertador, liberado == $liberado)
	
	$preso : Preso(afectado==$liberado)
	$apresado : Apresar(apresado==$liberado)
	not Muerto(afectado==$liberado)
then 
	mensajes.add($apresado.getNombre() + " fue liberado gracias a " + $libertador.getNombre() + ".");
	retract($preso);
	retract($apresado);
	retract($liberar);
	insert(new Libre($liberado));
end



rule "0.7.1 Apresar personaje"
when
	$apresado : Personaje()
	$apresador : Personaje()
	Apresar(sujeto == $apresador, apresado ==$apresado)
	$libre : Libre(afectado==$apresado)
	not Preso(afectado == $apresado)
then
	mensajes.add($apresado.getNombre() + " ha sido apresado por " + $apresador.getNombre());
	retract($libre);
	insert(new Preso($apresado));
end

/*
rule "0.6.1 Liberar personaje"
when 
    $libertador : Personaje()
    $liberado : Personaje()
    $libera : Liberar(sujeto == $libertador, liberado == $liberado)
    $preso : Preso(afectado==$liberado)
then 
    retract($preso);
    insert(new Libre($liberado));
end
rule "0.6.2 Liberar personaje apresado"
when 
    $libertador : Personaje()
    $liberado : Personaje()
    $apresador : Personaje()
    $libera : Liberar(sujeto == $libertador, liberado == $liberado)
    $apresa : Apresar(sujeto == $apresador, apresado ==$liberado)
    $preso : Preso(afectado==$liberado)
then 
    retract($apresa);
    retract($preso);
    insert(new Libre($liberado));
end
rule "0.7.1 Apresar personaje"
when
    $apresado : Personaje()
    $apresador : Personaje()
    Apresar(sujeto == $apresador, apresado ==$apresado)
    $libre : Libre(afectado==$apresado)
then
    retract($libre);
    insert(new Preso($apresado));
end
rule "0.7.2 Apresar personaje que ha sido previamente liberado"
when
    $apresado : Personaje()
    $apresador : Personaje()
    $libertador : Personaje()
    Apresar(sujeto == $apresador, apresado ==$apresado)
    $libera : Liberar(sujeto == $libertador, liberado == $apresado)
    $libre : Libre(afectado==$apresado)
then
    retract($libre);
    retract($libera);
    insert(new Preso($apresado));

end
*/

rule "0.8.1 Lootear personaje"
when 	
	$sujeto : Personaje()
	$looteado : Personaje()
	$loot : Lootear(sujeto == $sujeto, looteado == $looteado)
	$muertoPosee : List() from collect (Poseer(sujeto == $looteado))
then
	for(Poseer posesion : (List<Poseer>) $muertoPosee){
		mensajes.add($sujeto.getNombre() + " ha obtenido " + posesion.getNombreObjeto());
		insert(new Poseer($sujeto, posesion.getPoseido()));
		retract(posesion);
	}
	retract($loot);
end


rule "0.10 Rescatar a los apresador por "
when
	$perDerrotado: Personaje()
	$heroe : Personaje()
	$rescatar : RescatarDe(sujeto==$heroe, apresador==$perDerrotado)
	$rescatados : List() from collect(Apresar(sujeto==$perDerrotado))
then
	for(Apresar a : (List<Apresar>) $rescatados){
		mensajes.add( a.getApresado().getNombre() + " ha sido rescatado");
		insert(new Liberar($heroe, a.getApresado()));
	}
	retract($rescatar)
end



// REGLAS SEMIESPECIFICA ---------------------------------------------------------------------------------------------------------------------


rule "2.1.1.1 Heroe escapa del laberinto con el hilo tras matar al minotauro"
when
	//acoplados metido en hechos iniciales para que no de problemas
	//se crea esa variable para utilizarla mientras haya gente presa por minos
	//y haya que liberarlos porque el heroe tiene el hilo de Ariadna
	
	$h : Heroe()
	$minos :  Humano(nombre == Constantes.MINOS)
	$icaro : Humano(nombre == Constantes.ICARO)
	$dedalo : Humano(nombre == Constantes.DEDALO)
	$ariadna : Humano(nombre == Constantes.ARIADNA)
	$hPreso: Apresar(sujeto  == $minos, apresado == $h)
	$minotauro : Criatura( nombre ==  Constantes.MINOTAURO)
	$hiloAriadna : Objeto(nombre == Constantes.HILO_ARIADNA)
	Poseer(sujeto == $h, poseido == $hiloAriadna)
	Asesinar(sujeto  == $h, asesinado ==  $minotauro)
then
	mensajes.add($h.getNombre() + " escapa del laberinto con el Hilo de Ariadna.");
	insert (new TieneEnojoCon($minos, $icaro));
	insert (new TieneEnojoCon($minos, $dedalo));
	insert(new Liberar($ariadna, $h));
	insert(new RescatarDe($h,$minos));
end


rule "2.1.2 Heroe escapa del laberinto con vuelo tras matar al minotauro"
when
	$h : Heroe()
	$minos :  Humano( nombre ==  Constantes.MINOS  )
	$icaro : Humano( nombre ==  Constantes.ICARO  )
	$dedalo : Humano( nombre ==  Constantes.DEDALO  )
	Apresar(sujeto == $minos, apresado == $h)
	$minotauro : Criatura( nombre ==  Constantes.MINOTAURO)
	$objvuelo : Poseer(sujeto == $h, capacidadDe == Constantes.CAPACIDAD_VUELO)
	Asesinar(sujeto == $h, asesinado ==  $minotauro)
then
	mensajes.add($h.getNombre()+" escapa del laberinto con "+ $objvuelo.getNombreObjeto());
	insert (new TieneEnojoCon( $minos, $icaro));
	insert (new TieneEnojoCon( $minos, $dedalo));
	insert(new Liberar($h, $h));

end


rule "2.2 Enojo de Minos, apresa en el laberinto"
when
	$p : Personaje()
	$minos : Humano(nombre == Constantes.MINOS)
	TieneEnojoCon(sujeto == $minos, enojado == $p)
	not Muerto(afectado == $p)
then
	//mensajes.add($p.getNombre() + " ha sido apresado en el laberinto de Minos");
	insert(new Apresar($minos, $p));
end

rule "2.3 Heroe puede robar el hilo de Ariadana gracias a la capacidad de invisibilidad si no posee su favor"
salience 1
when
	$h : Heroe()
	$ariadna : Humano(nombre == Constantes.ARIADNA)
	$minos : Humano(nombre == Constantes.MINOS)
	$hilo : Objeto(nombre == Constantes.HILO_ARIADNA)
	$poseeHilo : Poseer(sujeto==$ariadna, poseido==$hilo)
	$poseeInv : Poseer(sujeto==$h, capacidadDe == Constantes.CAPACIDAD_INVISIBILIDAD)
	Apresar(sujeto==$minos, apresado==$h) 
then
	mensajes.add($h.getNombre() + " asalta a Ariadna y roba su hilo magico siendo invisible gracias a el "+ $poseeInv.getNombreObjeto());
	insert(new Poseer($h, $hilo));
	retract($poseeHilo);
end

rule "2.7 Si heroe tiene piel de minotauro y favor de hefesto construye rinionera de piel minotauro"	
when
	$h : Heroe()
	$pielMin : Objeto(nombre == Constantes.PIEL_MINOTAURO)
	$poseePiel : Poseer(sujeto==$h, poseido==$pielMin)
	$hefesto : Dios(nombre == Constantes.HEFESTO)
	FavoreceA(sujeto==$hefesto, favorecido==$h)
	$tote : Objeto(nombre == Constantes.TOTE_MINOTAURO)
then
	retract($poseePiel)
	insert (new Poseer($h, $tote));
	mensajes.add("Gracias al favor de hefesto y a la piel de Minotauro hefesto manufactura una Totebag de piel de Minotauro para "+ $h.getNombre());
end

rule "3.1 Si heroe tiene favor de apolo puede usar el oraculo de delfos para localizar a medusa"
when 
	$h : Heroe()
	$apolo : Dios(nombre==Constantes.APOLO)
	favorApolo : FavoreceA(sujeto==$apolo, favorecido==$h)
	$medusa : Criatura(nombre==Constantes.MEDUSA)
	not Preso(afectado==$h)
then
	mensajes.add($h.getNombre() + " localiza a Medusa tras asistir al Oraculo de Delfos teniendo el favor de Apolo.");
	insert(new Localizar($h, $medusa));
end



// REGLAS ESPECIFICAS ---------------------------------------------------------------------------------------------------------------------

rule "1.2.1 Enojo de Poseidon hacia Casiopea"
salience 1
when
    $poseidon : Dios(nombre == Constantes.POSEIDON)
    $casiopea : Humano(nombre == Constantes.CASIOPEA)
    $ceto : Criatura(nombre == Constantes.CETO)
    $cetoPreso : Preso(afectado == $ceto) 
    TieneEnojoCon(sujeto == $poseidon, enojado == $casiopea)
then
    mensajes.add("Ceto queda libre debido al enojo de Poseidon hacia Casiopea.");
    insert(new Liberar($poseidon, $ceto));
end

rule "1.2.3 Obtiene Capacidad de Reflexion con Escudo Espejo"
when
    $heroe : Heroe()
    $hefesto : Dios(nombre == Constantes.HEFESTO)
    $escudoBronce : Objeto(nombre == Constantes.ESCUDO_DE_BRONCE)
    Poseer(sujeto == $heroe, poseido == $escudoBronce)
    FavoreceA(sujeto == $hefesto, favorecido == $heroe)
     $escudoEspejo : Objeto(nombre == Constantes.ESCUDO_ESPEJO)
then
    mensajes.add( $heroe.getNombre() + " obtiene el Escudo Espejo gracias al favor de Hefesto, que pule el Escudo de Bronce.");
    insert(new Poseer($heroe ,$escudoEspejo));
    retract($escudoBronce);
    
    
end


rule "1.2.4 Localizacion de las Grayas por favores de Hermes y Atenea"
when
    $heroe : Heroe()
    $hermes : Dios(nombre == Constantes.HERMES)
    $atenea : Dios(nombre == Constantes.ATENEA)
    $grayas : Dios(nombre == Constantes.GRAYAS)
    FavoreceA(sujeto == $hermes, favorecido == $heroe)
    FavoreceA(sujeto == $atenea, favorecido == $heroe)
    
then
    mensajes.add($heroe.getNombre() + " localiza a las Grayas debido a los favores de Hermes y Atenea.");
    insert(new Localizar($heroe, $grayas));
end



rule "1.2.5 Chantaje a las grayas para localizar a las ninfas y medusa"
when
    $heroe : Heroe()
    $grayas : Dios(nombre == Constantes.GRAYAS)
    $gorgonas : Criatura(nombre == Constantes.GORGONAS)
    $ojoGrayas : Objeto(nombre == Constantes.OJO_DE_GRAYAS)
    $poseeOjo : Poseer(sujeto == $heroe, poseido == $ojoGrayas )
    $ninfas : Semidios(nombre == Constantes.LAS_NINFAS)   	
    $medusa : Criatura(nombre == Constantes.MEDUSA)
then
    mensajes.add($heroe.getNombre()+" localiza a las Ninfas y a Medusa tras chantajear a las Grayas ");
    //Posible implementacion de intercambio???
    retract($poseeOjo);
    //insert(new Poseer($grayas, $ojoGrayas)); es realmente necesario para la historia devolverselo? si no lo devolvemos evitamos el bucle
    insert(new Localizar($heroe, $ninfas));
    insert(new Localizar($heroe, $medusa));
end


rule "1.2.6 Robar ojo grayas"
when
	$heroe : Heroe()
	$grayas : Dios(nombre == Constantes.GRAYAS)
	Localizar(sujeto == $heroe, persLocalizado == $grayas)
    $ojoGrayas : Objeto(nombre == Constantes.OJO_DE_GRAYAS)
    $poseeOjo : Poseer(sujeto == $grayas, poseido == $ojoGrayas )
    Poseer(sujeto == $heroe, capacidadDe  == Constantes.CAPACIDAD_INVISIBILIDAD)
then
	mensajes.add($heroe.getNombre()+" roba el ojo a las Grayas.");
	retract($poseeOjo);
	insert(new Poseer($heroe, $ojoGrayas));
end
	
    


rule "1.2.7.1 Obtencion Sandalias Aladas tras localizar Ninfas"
when
    $heroe : Heroe()
    $ninfas : Semidios(nombre == Constantes.LAS_NINFAS)
   	$medusa : Criatura(nombre == Constantes.MEDUSA)
    $sandaliasAladas : Objeto(nombre == Constantes.SANDALIAS_ALADAS)
    $poseeNinfasS : Poseer(sujeto == $ninfas, poseido == $sandaliasAladas)
    Localizar(sujeto == $heroe, persLocalizado == $ninfas )
    not Muerto(afectado == $medusa)
then
    mensajes.add($heroe.getNombre() + " recibe de las Ninfas las Sandalias Aladas.");
    retract($poseeNinfasS);
    insert(new Poseer($heroe, $sandaliasAladas));
end



rule "1.2.7.2 Obtencion Zurron Magico tras localizar Ninfas"
when
    $heroe : Heroe()
    $ninfas : Semidios(nombre == Constantes.LAS_NINFAS)
   	$medusa : Criatura(nombre == Constantes.MEDUSA)
    $zurronMagico : Objeto(nombre == Constantes.ZURRON_MAGICO)
    $poseeNinfasZ : Poseer(sujeto == $ninfas, poseido == $zurronMagico)
    Localizar(sujeto == $heroe, persLocalizado == $ninfas )
    not Muerto(afectado == $medusa)
    
then
    mensajes.add($heroe.getNombre() + " recibe de las Ninfas el Zurron Magico.");
    retract($poseeNinfasZ);
    insert(new Poseer($heroe, $zurronMagico));
end


//SUPERAR O MORIR, GORGONAS
rule "1.2.8 Heroe evita  a las gorgonas"
when
	    $heroe : Heroe()
	    $g : Criatura(nombre == Constantes.GORGONAS)
	    $medusa : Criatura(nombre == Constantes.MEDUSA)
	    Localizar(sujeto==$heroe , persLocalizado==$medusa)
	 
	    $obj1 : Poseer(sujeto == $heroe, capacidadDe   == Constantes.CAPACIDAD_INVISIBILIDAD)
	    not Muerto(afectado == $g)
	    not Preso(afectado==$heroe)
then
		mensajes.add($heroe.getNombre() + " evita a las Gorgonas y entra a la cueva gracias a tener "+ $obj1.getNombreObjeto() + ".");
		insert(new Muerto($g)); //Las ponemos en muerto aunque tecnicamenete no lo esten para evitar que se vuelva a disparar esta regla
		
end

rule "1.2.9 Heroe no evita  a las gorgonas"
when
	    $heroe : Heroe()
	    $g : Criatura(nombre == Constantes.GORGONAS)
	   	$medusa : Criatura(nombre == Constantes.MEDUSA)
	    Localizar(sujeto==$heroe , persLocalizado==$medusa)
	 
	    not Poseer(sujeto == $heroe, capacidadDe   == Constantes.CAPACIDAD_INVISIBILIDAD)
	    not Muerto(afectado == $g)
	    Libre(afectado==$heroe)
then
		mensajes.add($heroe.getNombre() + " no consigue evitar a las Gorgonas debido a no tener un objeto que lo haga invisible.");
		insert(new Muerto($heroe)); 
end


//SUPERAR O MORIR, MEDUSA
rule "1.2.10.1 Heroe mata a Medusa"
when
    $heroe : Heroe()
    $m : Criatura(nombre == Constantes.MEDUSA)
    $g : Criatura(nombre == Constantes.GORGONAS)
    Muerto(afectado == $g)			//Tiene que haberlas superado para llegar a medusa
    Localizar(sujeto==$heroe, persLocalizado==$m)
    $vuelo : Poseer(sujeto == $heroe, capacidadDe == Constantes.CAPACIDAD_VUELO)
    $obj3 : Poseer(sujeto == $heroe, capacidadDe == Constantes.CAPACIDAD_REFLEXION)
    $obj4 : Poseer(sujeto == $heroe, capacidadDe   == Constantes.CAPACIDAD_MATAR)
    $obj5 : Poseer(sujeto == $heroe, capacidadDe   == Constantes.CAPACIDAD_GUARDAR_CABEZA_MEDUSA)
    not Muerto(afectado == $m)

then
	String objetos  =  $obj3.getNombreObjeto() + " y "+ $obj4.getNombreObjeto();
    mensajes.add($heroe.getNombre() + " mata a Medusa debido a tener " + objetos);
    mensajes.add($heroe.getNombre() + " guarda la peligrosa cabeza de Medusa en "+ $obj5.getNombreObjeto());
    mensajes.add($heroe.getNombre() + " sale del lugar volando gracias a "+ $vuelo.getNombreObjeto());
    insert(new Asesinar($heroe, $m));
    insert(new RescatarDe($heroe, $m));
    
end

rule "1.2.10.2 Lo mata medusa debido a que no tiene objeto reflexion"
when
	$heroe : Heroe()
	$medusa : Criatura(nombre == Constantes.MEDUSA)
	$g : Criatura(nombre == Constantes.GORGONAS)
	Muerto(afectado == $g)
	Localizar(sujeto==$heroe, persLocalizado==$medusa)
    not Poseer(sujeto == $heroe, capacidadDe == Constantes.CAPACIDAD_REFLEXION)
    not Muerto(afectado==$medusa)
then
	mensajes.add($heroe.getNombre() + " petrificado por Medusa debido a no tener un objeto que refleje");
	insert(new Asesinar($medusa,$heroe));
end

rule "1.2.10.3 Lo mata medusa debido a que no tiene objeto matar"
when
	$heroe : Heroe()
	$medusa : Criatura(nombre == Constantes.MEDUSA)
	$g : Criatura(nombre == Constantes.GORGONAS)
	Muerto(afectado == $g)			
	Localizar(sujeto==$heroe, persLocalizado==$medusa)
    not Poseer(sujeto == $heroe, capacidadDe == Constantes.CAPACIDAD_MATAR)
    not Muerto(afectado==$medusa)
then
	mensajes.add($heroe.getNombre() + " muere al intentar matar a Medusa ya que no posee un objeto con dicha capacidad");
	insert(new Asesinar($medusa,$heroe));

end


rule "1.2.10.4 Muere debido a que no tiene objeto para contener cabeza medusa"
when
	$heroe : Heroe()
	$medusa : Criatura(nombre == Constantes.MEDUSA)
	$asesinar: Asesinar(sujeto==$heroe, asesinado==$medusa)
	$medusaMuerta : Muerto(afectado == $medusa)
    not Poseer(sujeto == $heroe, capacidadDe == Constantes.CAPACIDAD_GUARDAR_CABEZA_MEDUSA)
then
	mensajes.add($heroe.getNombre() + " muerte al matar a Medusa y no guardar la cabeza en un objeto que pueda contenerla.");
	insert(new Asesinar($medusa,$heroe));
end

rule "1.2.10.5 No puede volver a casa tras matar a Medusa"
when
	$heroe : Heroe()
	$medusa : Criatura(nombre == Constantes.MEDUSA)
	$asesinar: Asesinar(sujeto==$heroe, asesinado==$medusa)
	$medusaMuerta : Muerto(afectado == $medusa)
    not Poseer(sujeto == $heroe, poseido.capacidadDe == Constantes.CAPACIDAD_VUELO)
then
	mensajes.add($heroe.getNombre() + " nunca consiguio volver a casa al no poder volar y acabo muriendo de inanicion");
	insert(new Muerto($heroe));
end


rule "1.2.11.1 Andromeda capturada por Ceto"
when
    $andromeda : Humano(nombre == Constantes.ANDROMEDA)
    $ceto : Criatura(nombre == Constantes.CETO)
    $andromedaLibre : Libre(afectado == $andromeda)
	$cetoLibre : Libre(afectado == $ceto)
	not Muerto( afectado ==  $ceto )
then
    mensajes.add("Andromeda queda presa debido a que Ceto esta libre y la captura.");
    insert (new Apresar($ceto, $andromeda));
end


rule "1.2.11.2 Derrotar Ceto con obj de petrificacion"
when
    $heroe : Heroe()
    $ceto : Criatura(nombre == Constantes.CETO)
    $objPetrificar : Poseer(sujeto == $heroe, capacidadDe== Constantes.CAPACIDAD_PETRIFICACION)
    not Muerto(afectado == $ceto )
then
    mensajes.add($heroe.getNombre() + " derrota a Ceto usando la "+$objPetrificar.getNombreObjeto()+".");
    insert(new Asesinar($heroe, $ceto));
    insert(new RescatarDe($heroe, $ceto));
end




rule "2.3 Muerte del minotauro"
when 
	$h : Heroe()
	$minotauro : Criatura( nombre == Constantes.MINOTAURO)
	$objeto : Poseer(sujeto == $h, capacidadDe == Constantes.CAPACIDAD_MATAR)
	Apresar(apresado==$h)
	Preso(afectado == $h) 
	not Muerto(afectado == $minotauro)
then
	mensajes.add($h.getNombre() + " consigue derrotar al Minotauro gracias a " + $objeto.getNombreObjeto());
	insert(new Asesinar($h, $minotauro));
end

rule "2.4.1 Dedalo obtiene la cera y las plumas tras ser apresado en el laberinto"
when
	$dedalo : Humano(nombre == Constantes.DEDALO)
	$minos : Humano(nombre == Constantes.MINOS)
	Apresar(sujeto == $minos, apresado == $dedalo)
	$cera : Objeto(nombre == Constantes.CERA_ABEJA)
	$plumas : Objeto(nombre == Constantes.PLUMAS)
then
	mensajes.add("Dedalo encuentra casualmente cera de abeja y plumas en el laberinto.");
	insert (new Poseer($dedalo,$plumas));
    insert (new Poseer($dedalo,$cera));
end

rule "2.4.2 Dedalo fabrica las alas para el y los demas apresados"
when
	$dedalo : Humano(nombre == Constantes.DEDALO)
	$minos : Humano(nombre == Constantes.MINOS)
	$cera : Objeto(nombre == Constantes.CERA_ABEJA)
	$plumas : Objeto(nombre == Constantes.PLUMAS)
	Apresar(sujeto == $minos, apresado == $dedalo)
	Poseer(sujeto == $dedalo, poseido == $plumas)
    Poseer(sujeto == $dedalo, poseido == $cera)
    $apresados : List() from collect(Apresar(sujeto==$minos))
    $alas : Objeto(nombre == Constantes.ALAS_CERA_PLUMAS)
then
	mensajes.add("Dedalo fabrica las alas a partir de cera y plumas");
	for(Apresar a : (List<Apresar>) $apresados){
		mensajes.add(a.getApresado().getNombre() + " ha obtenido las Alas de cera y pluma.");
		insert(new Poseer(a.getApresado(), $alas));
	}
end

//Podra huir cualquier personaje que tenga la capacidad de vuelo, este en el laberinto (apresado por minos) y no este muerto
rule "2.5.1 Personaje escapa del laberinto si tiene capacidad de vuelo"
when
	$per : Personaje()
	$minos : Humano(nombre == Constantes.MINOS)
	$puedeVolar : Poseer(sujeto==$per, poseido.capacidadDe==Constantes.CAPACIDAD_VUELO)
	Apresar(sujeto==$minos, apresado==$per)
	not Muerto(afectado==$per)
then
	//QUIEN LOS LIBERA???
	mensajes.add($per.getNombre() + " escapa del laberinto usando " + $puedeVolar.getNombreObjeto());
	insert(new Liberar($per, $per));
end

rule "2.5.2 Icaro muere al intentar escapar con las alas de cera y pluma"
when
	$icaro : Humano(nombre==Constantes.ICARO)
	$alas : Objeto(nombre == Constantes.ALAS_CERA_PLUMAS)
	Poseer(sujeto == $icaro, poseido == $alas)
	Libre(afectado==$icaro)
	not Muerto(afectado==$icaro)
then
	mensajes.add("Icaro decide no seguir los consejos de su padre y vuela demasiado cerca del sol, las Alas de cera se derriten e Icaro cae y muere.");
	insert(new Muerto($icaro));
end

rule "2.6 Heroe encuentra las alas de icaro"
when 
	$icaro : Humano(nombre==Constantes.ICARO)
	$alas : Objeto(nombre == Constantes.ALAS_CERA_PLUMAS)
	$alasIcaro : Poseer(sujeto == $icaro, poseido == $alas)
	Libre(afectado==$icaro)
	Muerto(afectado==$icaro)
	$h : Heroe()
then
	mensajes.add("Tras la muerte de Icaro, "+$h.getNombre() +" casualmente encuentra sus alas y se las queda.");
	retract($alasIcaro);
	insert(new Poseer($h, $alas));
end


rule "Fin Liberacion"
salience 3
when 
	$p1 : Personaje ()
	$p2 : Personaje ()
	Liberar(sujeto == $p1, liberado == $p2)
	$obj : Objetivo(sujeto == $p1, afectado == $p2)
then
	retract($obj);
end

rule "Fin Poseer"
salience 3
when 
	$p : Personaje ()
	$o : Objeto ()
	Poseer(sujeto == $p, poseido == $o)
	$obj : Objetivo(sujeto == $p, objeto == $o)
then
	retract($obj);
end

rule "Fin Capacidad"
salience 3
when 
    $p : Personaje()
    $o : Objeto()
    $obj : Objetivo(sujeto == $p, capacidadDe == $o.getCapacidadDe())
    Poseer(sujeto == $p, poseido == $o)
then
    retract($obj);
end




rule "FIN EJECUCION REGLAS"
salience 100
when
	$h : Heroe()
	not Objetivo(sujeto == $h)
then
	mensajes.add("STOP");
end





query "getPoseer" (Heroe heroe, Objeto objeto)
    $p : Poseer(sujeto == heroe, poseido == objeto)
  
end

query "getLiberar" (Personaje libertador, Personaje liberado )
    $l : Liberar(sujeto == libertador, liberado == liberado)
end





query "getPersonajeByName" (String name)
    $p : Personaje( nombre == name )
end

query "getObjetoByName" (String name)
    $o : Objeto( nombre == name )
end